buildscript {
    dependencies {
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}
plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}


version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

mixin {
    add sourceSets.main, "lightmanscurrency.refmap.json"
    config "lightmanscurrency.mixins.json"
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
    // It is REQUIRED to be set to true for this template to function.
    // See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
    copyIdeResources = true

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id

            //Fix other mods mixins
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'),
                    '--existing', file('optional_assets/1.20/biomesoplenty'),
                    '--existing', file('optional_assets/1.20/quark'),
                    '--existing', file('optional_assets/1.20/bwg')

            //Fix other mods mixins
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    //JEI repository when working on plugin
    /*maven {
    	name = "Progwm17 maven"
    	url = "https://dvs1.progwm16.com/files/maven/"
    }
    maven {
    	name = "ModMaven"
    	url = "https://modmaven.k-4u.nl"
    }*/
    //Geckolib
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        name = "JT Releases"
        url 'https://maven.jt-dev.tech/releases'
    }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.architectury.dev/" }
}

dependencies {

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    //JEI when actually working on plugin
    //implementation fg.deobf("mezz.jei:jei-1.19.4-common-api:13.1.0.2")
    //implementation fg.deobf("mezz.jei:jei-1.19.4-forge-api:13.1.0.2")
    //JEI when not working on plugin, to save compile/runClient time
    compileOnly fg.deobf("curse.maven:jei-238222:4595492")

    //REI
    compileOnly fg.deobf("me.shedaniel:RoughlyEnoughItems-api-forge:$rei_version")
    compileOnly fg.deobf("me.shedaniel:RoughlyEnoughItems-default-plugin-forge:$rei_version")
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:$cloth_config_version")
    implementation fg.deobf("dev.architectury:architectury-forge:$architectury_version")
    runtimeOnly fg.deobf("me.shedaniel:RoughlyEnoughItems-forge:$rei_version")

    //Curios API
    implementation fg.deobf("curse.maven:curios-309927:5843594")

    //Immersive Engineering API
    compileOnly fg.deobf("curse.maven:immersive-engineering-231951:4782978")

    compileOnly fg.deobf('curse.maven:lightmans-discord-integration-545459:4660940')

    //Claim Compats
    //FTB Chunks claim purchase compat
    compileOnly fg.deobf('curse.maven:ftb-chunks-forge-314906:4727985')
    compileOnly fg.deobf('curse.maven:ftb-teams-forge-404468:4623116')
    compileOnly fg.deobf('curse.maven:ftb-library-forge-404465:4720056')
    //compileOnly fg.deobf('curse.maven:architectury-api-419699:4663010')

    //Cadmus claim purchase compat
    compileOnly fg.deobf('curse.maven:cadmus-845832:4718413')
    compileOnly fg.deobf('curse.maven:resourceful-lib-570073:4927082')

    //Flan claim purchase compat
    compileOnly fg.deobf("curse.maven:flan-forge-493246:4920923")

    //Wood Compatibility (should be implementation for dataGen)
    //Biomes O' Plenty
    implementation fg.deobf("curse.maven:biomes-o-plenty-220318:4617259")
    implementation fg.deobf("curse.maven:terrablender-563928:5378180")
    //Quark
    implementation fg.deobf("curse.maven:quark-243121:5084013")
    implementation fg.deobf("curse.maven:zeta-968868:5078215")
    //BYG
    implementation fg.deobf("curse.maven:oh-the-biomes-weve-gone-1070751:5607559")
    implementation fg.deobf("corgitaco.corgilib:corgilib-forge:1.20.1-4.0.2.0")
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-1.20.1:4.4.7")
    implementation("com.eliotlash.mclib:mclib:20")
    implementation fg.deobf("curse.maven:oh-the-trees-youll-grow-962544:5540975")

    //Other Compats
    //Supplementaries RedMerchant compat
    compileOnly fg.deobf("curse.maven:supplementaries-412082:5068158")
    compileOnly fg.deobf("curse.maven:selene-499980:5068161")

    //Farming For Blockheads trader compat
    compileOnly fg.deobf("curse.maven:farming-for-blockheads-261924:4749360")
    compileOnly fg.deobf("curse.maven:balm-531761:5140912")

}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
        minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
        forge_version: forge_version, forge_version_range: forge_version_range,
        loader_version_range: loader_version_range,
        mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
        mod_authors: mod_authors, mod_description: mod_description,
        //Custom Properties
        ldi_version_range: ldi_version_range
]
processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}